plugins {
    id "java"
    id "com.moowork.node" version "1.2.0"
}

ext.compiledStylesheet = "build/tmp/css/stylesheet.css"

task cssCompile(type: NpmTask, dependsOn: npm_install) {
    def source = "src/main/resources/custom.css"
    inputs.file(source)
    outputs.file(compiledStylesheet)
    args = ["run", "postcss", "--", source, "-o", compiledStylesheet]
}

javadoc {
    dependsOn cssCompile
    title = "fun-tech meetup #5"
    options {
        overview = "src/main/resources/overview.html"
        stylesheetFile = new File(compiledStylesheet)
        header = new File("src/main/resources/header.html").text
    }
}

task serve {
    doLast {
        SimpleHttpFileServerFactory factory = new SimpleHttpFileServerFactory()
        HttpFileServer server = factory.start(new File(buildDir, "docs/javadoc"), 5000)
        println("Server Started on 5000 ctrc+c to kill it")
        java.lang.Thread.sleep(Long.MAX_VALUE);
    }
}
